pkgname=jj-git
pkgver=0.1.0
pkgrel=1
pkgdesc="Genuine JavaScript Object Notation processor."
arch=('x86_64')
url="https://github.com/taskie/quijine"
license=('Apache-2.0')
provides=("jj")
makedepends=('cargo' 'git')
optdepends=()
depends=()
conflicts=('jj')
source=("$pkgname::git+https://github.com/taskie/quijine")
sha1sums=('SKIP')

build() {
  cd "$pkgname"
  cd jj
  if command -v rustup > /dev/null 2>&1; then
    RUSTFLAGS="-C target-cpu=native" rustup run nightly \
      cargo build --release
  elif rustc --version | grep -q nightly; then
    RUSTFLAGS="-C target-cpu=native" \
      cargo build --release
  else
    cargo build --release
  fi
}

pkgver() {
  cd "$pkgname"
  local tag=$(git tag --sort=-v:refname | grep '^v[0-9]' | head -1)
  local commits_since=$(git rev-list "${tag}..HEAD" --count)
  echo "${tag##v}.r${commits_since}.$(git log --pretty=format:'%h' -n 1)"
}

package() {
  cd "$pkgname"
  install -Dm755 "target/release/jj" "$pkgdir/usr/bin/jj"
  install -Dm644 "jj/README.md" "$pkgdir/usr/share/doc/${pkgname}/README.md"

  install -Dm644 "jj/LICENSE" "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
